AC_INIT(Source/NSApplication.m)

# configure.in for GNUstep GUI Library
# Process this file with autoconf to produce a configure script.
#
#   Copyright (C) 1996 Free Software Foundation, Inc.
#
#   Written by:  Scott Christley <scottc@net-community.com>
#		 Ovidiu Predescu <ovidiu@net-community.com>
#
#   This file is part of the GNUstep GUI Library.
#
#   This library is free software; you can redistribute it and/or
#   modify it under the terms of the GNU Library General Public
#   License as published by the Free Software Foundation; either
#   version 2 of the License, or (at your option) any later version.
#   
#   This library is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
#   Library General Public License for more details.
#
#   You should have received a copy of the GNU Library General Public
#   License along with this library; see the file COPYING.LIB.
#   If not, write to the Free Software Foundation,
#   59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

#--------------------------------------------------------------------
# Use a .h file with #define's, instead of -D command-line switches
#--------------------------------------------------------------------
AC_CONFIG_HEADER(Headers/gnustep/gui/config.h)

#--------------------------------------------------------------------
# Determine the host, build, and target systems
#--------------------------------------------------------------------
AC_CANONICAL_SYSTEM

changequote(,)dnl
case "${host_cpu}" in
    i[45]86)	host_cpu=i386;;
    hppa1.1)	host_cpu=hppa;;
esac

case "${host_os}" in
    solaris2*)	host_os=solaris2;;
esac
changequote([,])dnl

HOST=$host
HOST_CPU=$host_cpu
HOST_VENDOR=$host_vendor
HOST_OS=$host_os

#--------------------------------------------------------------------
# Find the compiler
#--------------------------------------------------------------------
AC_PROG_CC
AC_PROG_CPP

#--------------------------------------------------------------------
# Find some programs
#--------------------------------------------------------------------
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_LN_S

#--------------------------------------------------------------------
# -pipe option for compiler
#--------------------------------------------------------------------
PIPE='-pipe'
AC_SUBST(PIPE)

#--------------------------------------------------------------------
# Standard ANSI headers
#--------------------------------------------------------------------
AC_HEADER_STDC

#--------------------------------------------------------------------
# Look for the X directory because the DPS related files can be there
# in case we are on a DPS native machine
#--------------------------------------------------------------------
AC_PATH_X
if test $HOST_OS = solaris2; then
    ac_x_includes=$ac_x_includes/X11
fi
CPPFLAGS="$CPPFLAGS -I$ac_x_includes"

#--------------------------------------------------------------------
# Look for the TIFF library
#--------------------------------------------------------------------
AC_CHECK_HEADERS(tiff.h)

if test "x$ac_cv_header_tiff_h" != "xyes"
then
   echo You must have the TIFF Graphics library installed.
   echo The most required version "(v3.4)" should be available at:
   echo
   echo ftp://ftp.sgi.com/graphics/tiff
   echo
   echo Configuration of the GNUstep GUI Library cannot continue.
   exit 1
fi

#--------------------------------------------------------------------
# Look for the DPSClient library
#--------------------------------------------------------------------
AC_CHECK_HEADERS(DPS/dpsclient.h)

if test "x$ac_cv_header_DPS_dpsclient_h" != "xyes"
then
   echo You must have the DPSClient library installed.
   echo The most recent public release, patched for GNUstep,
   echo should be available at:
   echo
   echo ftp://ftp.gnustep.org
   echo
   echo The most recent snapshot should be available at:
   echo
   echo ftp://alpha.gnu.ai.mit.edu/pub/gnustep
   echo
   echo Configuration of the GNUstep GUI Library cannot continue.
   exit 1
fi

#--------------------------------------------------------------------
# Operating system configuration
#--------------------------------------------------------------------
config_include=''
AC_SUBST(config_include)

# Checking for some additional libraries we might need
AC_CHECK_LIB(dl, dlopen)
AC_CHECK_LIB(pcthread, pthread_create,
	    ,
	    AC_CHECK_LIB(pthread, pthread_create))
AC_CHECK_LIB(m, sqrt)
AC_CHECK_LIB(nsl, chown)
AC_CHECK_LIB(socket, accept)

# Determine the Foundation library
AC_FIND_FOUNDATION

#--------------------------------------------------------------------
# Tools for making a DLL.
#--------------------------------------------------------------------
DLLTOOL='dlltool'
AC_SUBST(DLLTOOL)

#--------------------------------------------------------------------
# Make a static library?
# Import library is the library for linking with a DLL.
#--------------------------------------------------------------------
AC_ARG_ENABLE(static,
[  --disable-static        Do not build the static library],
echo $enable_static
if [[ x$enable_static = xno ]]; then
	STATIC_LIBRARY=''
else
	STATIC_LIBRARY='lib$(LIBRARY_NAME)$(libext)'
fi,
STATIC_LIBRARY='$(MAIN_FILE)$(libext)')
IMPORT_LIBRARY=$STATIC_LIBRARY
AC_SUBST(STATIC_LIBRARY)
AC_SUBST(IMPORT_LIBRARY)

#--------------------------------------------------------------------
# Make a shared library?
#--------------------------------------------------------------------
AC_ARG_ENABLE(shared,
[  --enable-shared         Build the library as a shared library],
        SHARED_LIBRARY='$(MAIN_FILE).so.$(VERSION)',
    SHARED_LIBRARY=)
AC_SUBST(SHARED_LIBRARY)

#--------------------------------------------------------------------
# Custom configuration based upon the target
#--------------------------------------------------------------------

# On some platforms, ld doesn't link in the complete library which
# can cause problems with the dynamic nature of ObjC.  Programs call
# methods for classes which don't exist in the executable!!
# The variables turn on/off the behaviour of ld to include the
# whole archive.
# Note that executable sizes will be larger.
# Note that these variables are passed to GCC not ld directly
whole_archive=''
no_whole_archive=''

case "${target}" in
  i[[345]]86-*-cygwin32)
        whole_archive='-Wl,--whole-archive'
	no_whole_archive='-Wl,--no-whole-archive'
	LIBS="$LIBS -ladvapi32"
	# -pipe not supported
	PIPE=''
	;;
  i[[345]]86-*-mingw32)
        whole_archive='-Wl,--whole-archive'
	no_whole_archive='-Wl,--no-whole-archive'
	LIBS="$LIBS -lwsock32 -ladvapi32"
	# Shared library is a DLL
	if [[ x$SHARED_LIBRARY != x ]]; then
		SHARED_LIBRARY='$(MAIN_FILE).dll'
		# Rename static library if also building a shared library
		if [[ x$STATIC_LIBRARY != x ]]; then
			STATIC_LIBRARY='$(MAIN_FILE)_s$(libext)'
			IMPORT_LIBRARY='$(MAIN_FILE)$(libext)'
		fi
	fi
	# -pipe not supported
	PIPE=''
	;;
esac

AC_SUBST(whole_archive)
AC_SUBST(no_whole_archive)

#--------------------------------------------------------------------
# Write the Makefiles and configuration files
#--------------------------------------------------------------------
AC_OUTPUT(Makefile Source/Makefile Testing/Makefile Documentation/Makefile Images/Makefile)

